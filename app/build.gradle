apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'stobix.app.lifetracker'
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        buildConfigField "boolean", "containers", "false"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    flavorDimensions "complexity", "stability"
    productFlavors {
        simple {
            dimension = "complexity"
            applicationIdSuffix ".simple"
            versionNameSuffix "(simple)"
        }
        containers {
            dimension = "complexity"
            applicationIdSuffix ".containers"
            versionNameSuffix "(containers)"
            buildConfigField "boolean", "containers", "true"
        }
        stable {
            dimension = "stability"
        }
        unstable {
            dimension = "stability"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildToolsVersion '28.0.3'
}


dependencies {
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Not compatible with com.android.support
    // implementation 'com.google.firebase:firebase-database:18.0.0'
    // implementation 'com.firebaseui:firebase-ui-auth:4.3.1'
    androidTestImplementation(
            'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // ColorPickerView
    //compile 'com.github.danielnilsson9:color-picker-view:1.4.0@aar'
    // Maybe better colorpicker
    //compile 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jjoe64:graphview:4.2.1'
    implementation 'de.codecrafters.tableview:tableview:2.8.0'
    testImplementation 'junit:junit:4.12'
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    androidTestImplementation "android.arch.persistence.room:testing:$room_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':utils')
    containersImplementation project(':containerview')
    implementation project(':compat')
}
repositories {
    mavenCentral()
}

apply plugin: 'kotlin-android-extensions'
// Not sure how to do this: Only applicable to simple, not containers
// apply plugin: 'com.google.gms.google-services'
